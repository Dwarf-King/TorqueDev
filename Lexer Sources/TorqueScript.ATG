/*

This file is part of TorqueDev.

TorqueDev is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by the 
Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

TorqueDev is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with TorqueDev.  If not, see <http://www.gnu.org/licenses>

EXCEPTIONS TO THE GPL: TorqueDev links in a number of third party libraries,
which are exempt from the license.  If you want to write closed-source
third party modules that you are going to link into TorqueDev, you may do so
without restriction.  I acknowledge that this technically allows for
one to bypass the open source provisions of the GPL, but the real goal
is to keep the core of TorqueDev free and open.  The rest is up to you.

*/
COMPILER TorqueScript


CHARACTERS
	letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_".
	digit = "0123456789".
	cr = '\r'.
	lf = '\n'.
	tab = '\t'.
	newline = cr + lf.

	globstart = '$'.
	locstart = '%'.
	regularString = ANY - '"' - '\\' - newline.
	taggedString = ANY - "'" - '\\' - newline.

	notDigit = ANY - digit.

TOKENS
	ident = letter {letter | digit}.
	/* variable = ('%' | '$') letter {letter | digit}. */

	stringToken = "\"" { regularString | "\\\\" | "\\\"" | "\\\'" | "\\c" | "\\n" | "\\t" 
				| "\\r" | "\\" letter | "\\" digit | "\\$" }
		      "\"".
	
	stringTaggedToken = "\'" { taggedString | "\\\\" | "\\\"" | "\\\'" | "\\c" | "\\n" | "\\t" 
				| "\\r" | "\\" letter | "\\" digit | "\\$" }
		      "\'".

	/* numberToken = digit {digit} | digit {digit} CONTEXT ("." notDigit). */

	/* floatToken = "." digit {digit} | digit {digit} "." digit {digit}. */

	allNumbers = (digit {digit} | digit {digit} CONTEXT ("." notDigit)) |
			("." digit {digit} | digit {digit} "." digit {digit}).
	
	globalVar = globstart letter {["::"] letter | digit}.
	localVar = locstart letter {["::"] letter | digit}.

COMMENTS FROM "/*" TO "*/"
COMMENTS FROM "//" TO lf

IGNORE cr + lf + tab


PRODUCTIONS
/*------------------------------------------------------------------------*/
/*			STATEMENTS					  */
/*------------------------------------------------------------------------*/
Statement
=
	Block
	| ";"
	| StatementExpr ";"
	| "if" "(" Expr ")" Statement ["else" Statement]
	| ("switch" | "switch$") "(" Expr ")" "{" {SwitchBlock} "}"
	| "while" "(" Expr ")" Statement
	| "for" "(" [StatementExpr] ";" [Expr] ";" [StatementExpr] ")" Statement
	| "break" ";"
	| "continue" ";"
	| "return" {Expr} ";"
	| "package" ident
	| "function" ident ["::" ident] "(" [Argument {"," Argument}] ")" Block
	| "datablock" (AssignOp Expr | (ident "(" [(ident | stringToken) [":" ident]] ")" Statement))
	| Particle
	/*  | "new" ident "(" [(ident | stringToken) [":" ident]] ")" Statement */
.	
/*------------------------------------------------------------------------*/
Block
=
	"{" {Statement} "}"
.
/*------------------------------------------------------------------------*/
StatementExpr
=
	UnaryOp 
	( Variable
	| AssignOp [Particle] Expr
	|
	)
.
/*------------------------------------------------------------------------*/
SwitchBlock
=
	SwitchLabel {SwitchLabel} [Statement {Statement}]
.
/*------------------------------------------------------------------------*/
SwitchLabel
=
	"case" Expr ":"
	|
	"default" ":"
.
/*------------------------------------------------------------------------*/
/*			EXPRESSIONS					  */
/*------------------------------------------------------------------------*/
Expr
=
	UnaryOp
	( Variable
	| (["!"] OrExpr ["?" Expr ":" Expr])
	| ( AssignOp [Particle] Expr)
	)
.
/*------------------------------------------------------------------------*/
Particle
=
	"new"
.
/*------------------------------------------------------------------------*/
UnaryOp
=
	{( "!" | "+" | "-" | "~" | "++" | "--" | "&" )}
	Primary
.
/*------------------------------------------------------------------------*/
Primary
=
	( ident
	| Literal
	| allNumbers
	| Variable
	| "(" Expr ")"
	)
	
	{ "++" 
	| "--" 
	| "::" ident
	| "." ident
	| "[" Expr {"," Expr} "]"				/* Arrays */
	| "(" [Argument [(":" ident) | ({"," Argument})]] ")" [Block]
	}
.
/*------------------------------------------------------------------------*/
Argument
=
	Expr
.
/*------------------------------------------------------------------------*/
AssignOp
=
	"=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" | ">>="
.
/*------------------------------------------------------------------------*/
OrExpr
=
	AndExpr {("||" | "or") UnaryOp AndExpr}
.
/*------------------------------------------------------------------------*/
AndExpr
=
	BitOrExpr {"&&" UnaryOp BitOrExpr}
.
/*------------------------------------------------------------------------*/
BitOrExpr
=
	BitXorExpr {"|" UnaryOp BitXorExpr}
.
/*------------------------------------------------------------------------*/
BitXorExpr
=
	BitAndExpr {"^" UnaryOp BitAndExpr}
.
/*------------------------------------------------------------------------*/
BitAndExpr
=
	StrEqlExpr {"&" UnaryOp EqlExpr}
.
/*------------------------------------------------------------------------*/
EqlExpr
=
	RelExpr {("!=" | "==") UnaryOp RelExpr}
.
/*------------------------------------------------------------------------*/
StrEqlExpr
=
	EqlExpr {( ["!"] "$=" ) UnaryOp EqlExpr}
.
/*------------------------------------------------------------------------*/
RelExpr
=
	ShiftExpr {("<" | ">" | "<=" | ">=") UnaryOp ShiftExpr}
.
/*------------------------------------------------------------------------*/
ShiftExpr
=
	AddExpr {("<<" | ">>") UnaryOp AddExpr}
.
/*------------------------------------------------------------------------*/
AddExpr
=
	MulExpr {("+" | "-" | "@" | "TAB" | "NL" | "SPC" ) UnaryOp MulExpr}
.
/*------------------------------------------------------------------------*/
MulExpr
=
	{("*" | "/" | "%") UnaryOp}
.
/*------------------------------------------------------------------------*/
Literal
=
	stringToken | stringTaggedToken | "true" | "false".
/*------------------------------------------------------------------------*/
Variable
=
	localVar | globalVar.
/*------------------------------------------------------------------------*/









TorqueScript
=
	{ Statement }
.

END TorqueScript.
